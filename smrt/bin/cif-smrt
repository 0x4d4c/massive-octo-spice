#!perl

BEGIN {
    use FindBin;
    use local::lib "$FindBin::Bin/..";
}

use 5.014002;
use feature 'say';
use strict;
use warnings;

use AnyEvent;
use Daemon::Control;
use Getopt::Long;
use Config::Simple;
use CIF qw/debug hash_create_random init_logging $Logger/;
use CIF::Smrt;
use Data::Dumper;

my $help;
my $remote      = 'zmq+tcp://localhost:'.CIF::DEFAULT_PORT();
my $token       = '';
my $rule        = $CIF::SmrtRulesDefault;
my $feed        = '';
my $limit       = 0;
my $refresh     = 0;
my $meta        = 0;
my $test_mode   = 0;
my $clean       = 0;
my $notbefore   = 'today';
my $cache       = $CIF::VarPath.'/smrt/cache';

# daemon
my $daemon      = 0;
my $config      = $CIF::EtcPath.'/cif-smrt.conf';
my $pidfile     = $CIF::VarPath.'/smrt.pid';
my $user        = $CIF::CIF_USER;
my $group       = $CIF::CIF_GROUP;
my $interval    = 60; #min
my $randomstart = 30; #min

# notification
my $notify;
my $notify_to       = 'root@localhost';
my $notify_subj     = '[cif-smrt] ERROR';
my $notify_from     = 'cif';
my $notify_level    = 'error';

# logging
my $debug       = 0;
my $verbose     = 0;
my $logging     = 0;
my $consolemode = 0;
my $logfile     = $CIF::VarPath.'/log/cif-smrt.log';

my $today = DateTime->today()->ymd('');

my $smrt_config;
my $client_config;

if(-e $config){
    $config = Config::Simple->new($config);
    $client_config  = $config->get_block('client');
    $smrt_config    = $config->get_block('smrt');
    
    $token  = $smrt_config->{'token'}   || $client_config->{'token'}    || $token;
    $remote = $smrt_config->{'remote'}  || $client_config->{'remote'}   || $remote;
}

Getopt::Long::Configure("bundling");
GetOptions(
    # basic
    'help|h'        => \$help, 
    'config|C=s'    => \$config,
    
    # daemon opts
    'daemon|D'          => \$daemon,
    'user|u=s'          => \$user,
    'group|g=s'         => \$group,
    'pidfile|p=s'       => \$pidfile,
    'interval|i=i'      => \$interval,
    'randomstart|R=i'   => \$randomstart,
    
    # logging
    'logfile=s'     => \$logfile,
    'debug|d'       => \$debug,
    'verbosity|v'   => \$verbose,
    'logging'       => \$logging,
    'consolemode'   => \$consolemode,
    
    # advanced
    'remote|R=s'    => \$remote,
    'token|T=s'     => \$token,
    'rule|r=s'      => \$rule,
    'feed|f=s'      => \$feed,
    'limit|L=i'     => \$limit,
    'refresh|Z'     => \$refresh,
    'meta|M'        => \$meta,
    'test|x'        => \$test_mode,
    'clean|c'       => \$clean,
    'nbefore|N=s'   => \$notbefore,
    'cache|P=s'     => \$cache,
    
    #notification
    'notify'        => \$notify,
    'notify-to=s'   => \$notify_to,
    'notify-subj=s' => \$notify_subj,
    'notify-from=s' => \$notify_from,
) or die(usage());

$debug = ($debug) ? 'INFO' : 'ERROR';
$debug = 'DEBUG' if($verbose);

if($notify){
    $notify = { 
        to          => $notify_to, 
        from        => $notify_from, 
        subject     => $notify_subj, 
        min_level   => lc($notify_level) }
}

init_logging(
    {
        level       => $debug, 
        filename    => ($logging) ? $logfile : 0,
    },
    $notify,   
);

die(usage()) if($help);
die(usage()) unless($rule);

die(usage()) unless(-f $rule || -d $rule);
die(usage()) if((-f $rule) && !$feed);

if($notbefore && $notbefore =~ /^\d+$/){
    print usage();
    print $0.' -N|--notbefore requires quotes around the value (eg: -N "3 days ago")'."\n\n";
    exit(-1);
}

sub usage {
    return <<EOF;

Usage: $0 [OPTIONS] [-D status|start|stop|restart|reload]

 Options:
    -C, --config=FILE       specify cofiguration file, default: $config
    -d, --debug             turn on debugging [INFO]
    -v, --verbosity         turn up debug verbosity [DEBUG]
    -h, --help              this message
     
    -r, --rule=STRING       specify a rule or a rules directory, default: $rule
    -f, --feed=STRING       specify a feed (within a rule)
    -R, --remote=STRING     specify a remote to connect to, default $remote
    -T, --token=STRING      specify a default token/apikey to use, default: $token
    
 Daemon Options:
    -D, --daemon            run as daemon
    -u, --user              run daemon as user, default: $user
    -g, --group             run daemon as group, default: $group
    -p, --pid               pidfile location, default: $pidfile
    --consolemode           run daemon in console mode (no-fork).
    
    -r, --randomstart       random start delay, default: $randomstart min
    -i, --interval          runtime interval, default: $interval min
    
    --logfile:              logfile location, default: $logfile
    --logging:              turn on logging [to file]
    
 Notification Options:
    --notify:               turn on notification, default: off.
    --notify-to:            default: $notify_to
    --notify-from:          default: $notify_from
    --notify-subj:          default: $notify_subj
    --notify-level:         default: $notify_level
    
 Advanced Options:
    -M, --meta              apply metadata processors, default: $meta
    -c, --clean             clear cache
    -x, --test              run in test mode
    -N, --notbefore         not before "[today|yesterday|X days ago]"
    -P, --cache             cache location, default $cache

 Examples:
    $0 -C $config
    $0 -C $config -p $pidfile -D start
    $0 -r $rule -D start

EOF
}

if($daemon){
    $ARGV[0] = 'start' if($consolemode && !$ARGV); # default set run_command if we're in console mode
                                                   # and forget to 'start'

    my $rv = Daemon::Control->new(
        name    => 'cif-smrt',
        program => sub { main() },
        
        pid_file    => $pidfile,
        fork        => ($consolemode) ? 0 : 2,
        
        user        => $user,
        group       => $group,
    )->run();
} else {
    _main()
}

$Logger->info('shutting down...');

sub main {
    my $done = AnyEvent->condvar();
    
    $randomstart = int(rand($randomstart)); # sometime in the next $randomstart
    $Logger->info('delaying start for: '.$randomstart.'min then running every '.$interval.'min there after...');
    $Logger->info('to run immediately, set: -R 0');
    
    $randomstart    = (60 * $randomstart);
    $interval       = (60 * $interval);
    
    my $w = AnyEvent->timer(
        after       => $randomstart, 
        interval    => $interval, # once an hour
        cb          => sub { 
            _main(); 
            $Logger->info('sleeping: '.($interval/60).'min'); 
        }
    );
    
    $done->recv();
}

sub _main { 
    _clean_tmp();
    
    my $smrt = CIF::Smrt->new({
        client_config => {
            remote          => $remote,
            Token           => $token,
            encoder_pretty  => ($debug ne 'ERROR') ? 1 : 0,
        },
        test_mode   => $test_mode,
    });

    my ($err,$ret);
    
    $ret = $smrt->ping_router();
    unless($ret){
        $Logger->error('router unavailable... ping timeout');
        return;
    }

    ##TODO refactor this into libcif rules?
    my $rules;
    if(-d $rule){
        opendir(F,$rule) || die('unable to open: '.$rule.'... '.$!);
        my $files = [ sort { $a cmp $b } grep (/.cfg$/,readdir(F)) ];
        foreach my $f (@$files){
            my $t = Config::Simple->new($rule.'/'.$f);
            my @sections = keys(%{$t->{'_DATA'}});
            next unless($t->param(-block => 'default')->{'enabled'});
            foreach my $section (@sections){
                next if($section =~ /^default/);
                next if($t->param(-block => $section)->{'disabled'});
                my $c = $t->param(-block => $section);
                push(@$rules,{
                    config  => $rule."/".$f,
                    feed    => $section,
                    override    => {
                        limit       => $limit,
                        not_before  => $notbefore,
                    },
                    meta        => (defined($c->{'meta'})) ? $c->{'meta'} : $meta,
                });
                    
            }
        }
                
    } else {
        $rules = [{
            config  => $rule,
            feed    => $feed,
            override    => {
                limit       => $limit,
                not_before  => $notbefore,
            },
            meta    => $meta,
        }];
    }
    
    foreach (@$rules){
        $Logger->info("processing: $0 -d -r $_->{'config'} -f $_->{'feed'}");
        $ret = $smrt->process({
            rule    => $_,
        });
        if($ret){
            ($err,$ret) = $smrt->get_client->submit({
                Observables => $ret,
            });
            croak($err) if($err);
        } else {
            $Logger->info('nothing [new] to send...');
        }
    }
    $Logger->info('done');    
}

##TODO re-factor
sub _clean_tmp {
    $Logger->info('cleaning up tmp...');
    opendir(F,$cache) || return;
    my $tmp = [ sort { $a cmp $b } grep (/^\d+\.log$/,readdir(F)) ];
    foreach (@$tmp){
        m/^(\d+)\.log$/;
        if($clean || ($1 < $today)){
            $Logger->info('removing: '.$cache.'/'.$_);
            #todo -- clean this
            system('rm -f '.$cache.'/'.$_);
        }
    }
}
